import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import "components"

Item {
    id: toolMode
    visible: globalState.isDesktopWidget
    // å¤–éƒ¨å¯¼å…¥çš„ç»„ä»¶
    property var loginStatusDialogs
    property var todoCategoryManager
    property var root

    
    property int spacing: 10 // å¼¹çª—ä¹‹é—´çš„é—´è·
    
    // åŠ¨æ€è®¡ç®—æ€»å®½åº¦å’Œé«˜åº¦
    property int totalWidth: 400  // å›ºå®šå®½åº¦
    property int totalHeight: {
        var height = titleBar.height;  // æ ‡é¢˜æ é«˜åº¦
        
        if (settingsPopup.visible) {
            height += spacing + settingsPopup.height;
        }
        if (addTaskPopup.visible) {
            height += spacing + addTaskPopup.height;
        }
        if (mainContentPopup.visible) {
            height += spacing + mainContentPopup.height;
        }
        if (todoItemDropdown.visible) {
            height += spacing + todoItemDropdown.height;
        }
        
        return height;
    }
    
    // å½“å°ºå¯¸å˜åŒ–æ—¶é€šçŸ¥çˆ¶çª—å£
    onTotalWidthChanged: {
        if (root && globalState.isDesktopWidget) {
            root.width = totalWidth;
        }
    }
    
    onTotalHeightChanged: {
        if (root && globalState.isDesktopWidget) {
            root.height = totalHeight;
        }
    }
    
    // ç›‘å¬å°ç»„ä»¶æ¨¡å¼çŠ¶æ€å˜åŒ–
    Connections {
        target: globalState
        function onIsDesktopWidgetChanged() {
            if (globalState.isDesktopWidget && root) {
                // åˆ‡æ¢åˆ°å°ç»„ä»¶æ¨¡å¼æ—¶ç«‹å³æ›´æ–°å°ºå¯¸
                root.width = totalWidth;
                root.height = totalHeight;
            }
        }
    }
    
    // ç»„ä»¶å®Œæˆæ—¶åˆå§‹åŒ–å°ºå¯¸
    Component.onCompleted: {
        if (globalState.isDesktopWidget && root) {
            root.width = totalWidth;
            root.height = totalHeight;
        }
    }

    // æ ‡é¢˜æ 
    Rectangle {
        id: titleBar
        anchors.top: parent.top
        width: 400
        height: 35
        color: ThemeManager.primaryColor
        border.width: 0                               ///< è¾¹æ¡†å®½åº¦
        radius: 5                                     ///< åœ†è§’
        visible: globalState.isDesktopWidget

        // çª—å£æ‹–æ‹½å¤„ç†åŒºåŸŸ
        WindowDragHandler {
            anchors.fill: parent
            targetWindow: toolMode.root
            enabled: !globalState.preventDragging
        }

        RowLayout {
            anchors.fill: parent
            anchors.leftMargin: 5   ///< å·¦è¾¹è·
            anchors.rightMargin: 5  ///< å³è¾¹è·

            IconButton {
                text: "\ue90f"              ///< èœå•å›¾æ ‡
                onClicked: globalState.toggleSettingsVisible()
            }

            /// å¾…åŠçŠ¶æ€æŒ‡ç¤ºå™¨
            Text {
                id: todoStatusIndicator
                Layout.alignment: Qt.AlignVCenter
                color: ThemeManager.textColor
                font.pixelSize: 14
                font.bold: true

                // TODO: æ–°å¢æˆ–åˆ é™¤å¾…åŠåï¼Œè¿™é‡Œçš„æ–‡å­—æ²¡æœ‰æ›´æ”¹ï¼ˆæ˜¾ç¤ºçš„æ˜¯å½“å‰åˆ†ç±»ä¸‹çš„å¾…åŠæ•°é‡ï¼‰
                property int todoCount: todoManager.rowCount()
                property bool isHovered: false

                text: {
                    if (isHovered) {
                        return todoCount > 0 ? todoCount + "ä¸ªå¾…åŠ" : "æ²¡æœ‰å¾…åŠ";
                    } else {
                        return todoCount > 0 ? todoCount + "ä¸ªå¾…åŠ" : "æˆ‘çš„å¾…åŠ";
                    }
                }

                MouseArea {
                    anchors.fill: parent
                    hoverEnabled: true
                    onEntered: parent.isHovered = true
                    onExited: parent.isHovered = false
                }
            }

            Item {
                Layout.fillWidth: true
            }

            /// ä»»åŠ¡åˆ—è¡¨å±•å¼€/æ”¶èµ·æŒ‰é’®
            IconButton {
                text: globalState.isShowTodos ? "\ue667" : "\ue669"     ///< æ ¹æ®çŠ¶æ€æ˜¾ç¤ºç®­å¤´
                onClicked: globalState.toggleTodosVisible()     ///< åˆ‡æ¢ä»»åŠ¡åˆ—è¡¨æ˜¾ç¤º
            }

            /// æ·»åŠ ä»»åŠ¡æŒ‰é’®
            IconButton {
                text: "\ue903"                                 ///< åŠ å·å›¾æ ‡
                onClicked: globalState.toggleAddTaskVisible()   ///< åˆ‡æ¢æ·»åŠ ä»»åŠ¡ç•Œé¢æ˜¾ç¤º
            }

            /// æ™®é€šæ¨¡å¼å’Œå°ç»„ä»¶æ¨¡å¼åˆ‡æ¢æŒ‰é’®
            IconButton {
                text: "\ue620"
                /// é¼ æ ‡æŒ‰ä¸‹äº‹ä»¶å¤„ç†
                onClicked: {
                    globalState.toggleWidgetMode();
                }
                fontSize: 18
            }
        }
    }

    // è®¾ç½®çª—å£
    Popup {
        id: settingsPopup
        y: titleBar.height + toolMode.spacing
        width: 400
        height: 250
        padding: 0 // æ¶ˆé™¤Popupå’ŒcontentItemä¹‹é—´çš„é—´éš™

        modal: false // éæ¨¡æ€ï¼Œå…è®¸åŒæ—¶æ‰“å¼€å¤šä¸ªå¼¹å‡ºçª—å£
        focus: true
        closePolicy: Popup.NoAutoClose
        visible: globalState.isDesktopWidget && globalState.isShowSetting

        contentItem: Rectangle {
            color: ThemeManager.secondaryBackgroundColor
            border.color: ThemeManager.borderColor
            border.width: 1
            radius: 5

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 10

                Label {
                    text: "è®¾ç½®"
                    font.bold: true
                    font.pixelSize: 16
                    color: ThemeManager.textColor
                }

                // è®¾ç½®å†…å®¹
                ControlRow {
                    id: darkModeCheckBox
                    text: qsTr("æ·±è‰²æ¨¡å¼")
                    checked: globalState.isDarkMode
                    enabled: !globalState.isFollowSystemDarkMode
                    leftMargin: 10
                    controlType: ControlRow.ControlType.Switch

                    onCheckedChanged: {
                        globalState.isDarkMode = checked;
                        // ä¿å­˜è®¾ç½®åˆ°é…ç½®æ–‡ä»¶
                        setting.save("setting/isDarkMode", checked);
                    }
                }

                ControlRow {
                    id: preventDraggingCheckBox
                    text: qsTr("é˜²æ­¢æ‹–åŠ¨çª—å£ï¼ˆå°çª—å£æ¨¡å¼ï¼‰")
                    checked: globalState.preventDragging
                    enabled: globalState.isDesktopWidget
                    leftMargin: 10
                    controlType: ControlRow.ControlType.Switch
                    onCheckedChanged: {
                        globalState.preventDragging = checked;
                        setting.save("setting/preventDragging", globalState.preventDragging);
                    }
                }

                ControlRow {
                    text: todoManager.isLoggedIn ? qsTr("è‡ªåŠ¨åŒæ­¥") : qsTr("è‡ªåŠ¨åŒæ­¥ï¼ˆæœªç™»å½•ï¼‰")
                    checked: todoSyncServer.isAutoSyncEnabled
                    leftMargin: 10
                    controlType: ControlRow.ControlType.Switch

                    onCheckedChanged: {
                        if (checked) {
                            // å¦‚æœæœªç™»å½•ï¼Œæ˜¾ç¤ºæç¤ºå¹¶é‡ç½®å¼€å…³
                            if (!todoManager.isLoggedIn) {
                                toggle();
                                settingPage.loginStatusDialogs.showLoginRequired();
                            } else {
                                todoSyncServer.setAutoSyncEnabled(checked);
                            }
                        }
                    }
                }
            }
        }
    }

    // æ·»åŠ ä»»åŠ¡çª—å£
    Popup {
        id: addTaskPopup

        property int calculatedY: {
            var baseY = titleBar.height + toolMode.spacing;
            if (settingsPopup.visible) {
                baseY += settingsPopup.height + toolMode.spacing;
            }
            return baseY;
        }

        y: calculatedY
        width: 400
        height: 250
        modal: false // éæ¨¡æ€ï¼Œå…è®¸åŒæ—¶æ‰“å¼€å¤šä¸ªå¼¹å‡ºçª—å£
        focus: true
        closePolicy: Popup.NoAutoClose
        visible: globalState.isDesktopWidget && globalState.isShowAddTask

        // TODO: è¿™é‡Œå¯ä»¥å¤©ç©ºæ ‡é¢˜è¾“å…¥æ¡†ï¼Œè¿˜æœ‰ä¸€äº›è¯¦ç»†è®¾ç½®
        contentItem: Rectangle {
            color: ThemeManager.secondaryBackgroundColor
            border.color: ThemeManager.borderColor
            border.width: 1
            radius: 5

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 10

                Label {
                    text: "æ·»åŠ ä»»åŠ¡"
                    font.bold: true
                    font.pixelSize: 16
                    color: ThemeManager.textColor
                }

                ScrollView {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    clip: true
                }

                RowLayout {
                    Layout.fillWidth: true
                    Layout.alignment: Qt.AlignRight

                    CustomButton {
                        text: "æ·»åŠ "
                        onClicked: todoManager.addTodo(qsTr("æ–°çš„å¾…åŠäº‹é¡¹"))
                    }
                }
            }
        }
    }

    // ä¸»å†…å®¹åŒºçª—å£
    Popup {
        id: mainContentPopup

        property int baseHeight: 200
        property int calculatedY: {
            var baseY = titleBar.height + toolMode.spacing;
            if (settingsPopup.visible) {
                baseY += settingsPopup.height + toolMode.spacing;
            }
            if (addTaskPopup.visible) {
                baseY += addTaskPopup.height + toolMode.spacing;
            }
            return baseY;
        }
        y: calculatedY

        width: 400
        height: baseHeight
        modal: false // éæ¨¡æ€ï¼Œå…è®¸åŒæ—¶æ‰“å¼€å¤šä¸ªå¼¹å‡ºçª—å£
        focus: true
        closePolicy: Popup.NoAutoClose
        visible: globalState.isDesktopWidget && globalState.isShowTodos // åœ¨å°ç»„ä»¶æ¨¡å¼ä¸‹ä¸”éœ€è¦æ˜¾ç¤ºæ‰€æœ‰ä»»åŠ¡æ—¶æ˜¾ç¤º

        contentItem: Rectangle {
            color: ThemeManager.secondaryBackgroundColor
            border.color: ThemeManager.borderColor
            border.width: 1
            radius: 5

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 10

                Label {
                    text: "å¾…åŠä»»åŠ¡"
                    font.bold: true
                    font.pixelSize: 16
                    color: ThemeManager.textColor
                }

                // å¾…åŠåˆ—è¡¨
                ListView {
                    id: todoListPopupView
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    clip: true
                    model: todoManager

                    // ä¸‹æ‹‰åˆ·æ–°ç›¸å…³å±æ€§ä¸é€»è¾‘ï¼ˆåœ¨å°ç»„ä»¶æ¨¡å¼çš„å¼¹çª—ä¸­ï¼‰
                    property bool refreshing: false
                    property int pullThreshold: 50
                    property real pullDistance: 0

                    onContentYChanged: {
                        pullDistance = contentY < 0 ? -contentY : 0;
                    }
                    onMovementEnded: {
                        if (contentY < -pullThreshold && atYBeginning && !refreshing) {
                            refreshing = true;
                            todoManager.syncWithServer();
                        }
                    }

                    header: Item {
                        width: todoListPopupView.width
                        height: todoListPopupView.refreshing ? 45 : Math.min(45, todoListPopupView.pullDistance)
                        visible: height > 0 || todoListPopupView.refreshing
                        Column {
                            anchors.centerIn: parent
                            spacing: 4
                            BusyIndicator {
                                running: todoListPopupView.refreshing
                                visible: todoListPopupView.refreshing || todoListPopupView.pullDistance > 0
                                width: 18
                                height: 18
                            }
                            Label {
                                text: todoListPopupView.refreshing ? qsTr("æ­£åœ¨åŒæ­¥...") : (todoListPopupView.pullDistance >= todoListPopupView.pullThreshold ? qsTr("é‡Šæ”¾åˆ·æ–°") : qsTr("ä¸‹æ‹‰åˆ·æ–°"))
                                color: ThemeManager.textColor
                                font.pixelSize: 11
                            }
                        }
                    }

                    Connections {
                        target: todoManager
                        function onSyncStarted() {
                            if (!todoListPopupView.refreshing && todoListPopupView.atYBeginning) {
                                todoListPopupView.refreshing = true;
                            }
                        }
                        function onSyncCompleted(success, errorMessage) {
                            todoListPopupView.refreshing = false;
                            todoListPopupView.contentY = 0;
                        }
                    }

                    delegate: Rectangle {
                        width: todoListPopupView.width
                        height: 40
                        color: index % 2 === 0 ? ThemeManager.secondaryBackgroundColor : ThemeManager.backgroundColor

                        // ç‚¹å‡»é¡¹ç›®æ˜¾ç¤ºä¸‹æ‹‰çª—å£
                        MouseArea {
                            anchors.fill: parent
                            z: 0
                            onClicked: {
                                todoItemDropdown.currentTodoIndex = index;
                                todoItemDropdown.currentTodoData = {
                                    title: model.title,
                                    description: model.description,
                                    category: model.category,
                                    important: model.important
                                };
                                globalState.toggleDropdownVisible();
                            }
                        }

                        RowLayout {
                            anchors.fill: parent
                            anchors.margins: 5
                            spacing: 5

                            // å¾…åŠçŠ¶æ€æŒ‡ç¤ºå™¨
                            Rectangle {
                                width: 16
                                height: 16
                                radius: 8
                                color: model.isCompleted ? ThemeManager.completedColor : ThemeManager.lowImportantColor

                                MouseArea {
                                    anchors.fill: parent
                                    onClicked: {
                                        todoManager.markAsDone(index);
                                        mouse.accepted = true;  // é˜»æ­¢äº‹ä»¶ä¼ æ’­
                                    }
                                }
                            }

                            // å¾…åŠæ ‡é¢˜
                            Label {
                                text: model.title
                                color: ThemeManager.textColor
                                Layout.fillWidth: true
                            }

                            // åˆ é™¤æŒ‰é’®
                            Rectangle {
                                width: 30
                                height: 30
                                color: "transparent"
                                border.width: 0
                                Text {
                                    anchors.centerIn: parent
                                    text: "ğŸ—‘"
                                    color: "gray"
                                    font.pixelSize: 14
                                }
                                MouseArea {
                                    anchors.fill: parent
                                    onClicked: {
                                        todoManager.removeTodo(index);
                                    }
                                }
                            }
                        }
                    }

                    // ä¸ºå¼¹å‡ºåˆ—è¡¨æ·»åŠ æ»šåŠ¨æ¡
                    ScrollBar.vertical: ScrollBar {}
                }
            }
        }
    }

    // å¾…åŠäº‹é¡¹è¯¦æƒ…çª—å£
    Popup {
        id: todoItemDropdown

       property int calculatedY: {
            var baseY = titleBar.height + toolMode.spacing;
            if (settingsPopup.visible) {
                baseY += settingsPopup.height + toolMode.spacing;
            }
            if (addTaskPopup.visible) {
                baseY += addTaskPopup.height + toolMode.spacing;
            }
            if (mainContentPopup.visible) {
                baseY += mainContentPopup.height + toolMode.spacing;
            }
            return baseY;
        }
        y: calculatedY
        width: mainContentPopup.width
        height: 180
        modal: false
        focus: true
        closePolicy: Popup.CloseOnEscape | Popup.CloseOnPressOutside
        visible: globalState.isDesktopWidget && globalState.isShowDropdown

        property int currentTodoIndex: -1
        property var currentTodoData: null

        contentItem: Rectangle {
            color: ThemeManager.secondaryBackgroundColor
            border.color: ThemeManager.borderColor
            border.width: 1
            radius: 5

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 8

                // é¡¶éƒ¨æ ‡é¢˜æ å’Œæ”¶å›æŒ‰é’®
                RowLayout {
                    Layout.fillWidth: true
                    spacing: 10

                    Label {
                        text: todoItemDropdown.currentTodoData ? todoItemDropdown.currentTodoData.title : ""
                        font.bold: true
                        font.pixelSize: 14
                        color: ThemeManager.textColor
                        Layout.fillWidth: true
                        elide: Text.ElideRight
                    }

                    // æ”¶å›æŒ‰é’®
                    Rectangle {
                        width: 24
                        height: 24
                        color: "transparent"
                        border.width: 0
                        radius: 12

                        Text {
                            anchors.centerIn: parent
                            text: "^"
                            color: ThemeManager.textColor
                            font.pixelSize: 16
                            font.bold: true
                        }

                        MouseArea {
                            anchors.fill: parent
                            hoverEnabled: true
                            onClicked: {
                                globalState.toggleDropdownVisible();
                            }
                            onEntered: {
                                parent.color = ThemeManager.borderColor;
                            }
                            onExited: {
                                parent.color = "transparent";
                            }
                        }
                    }
                }

                Rectangle {
                    Layout.fillWidth: true
                    height: 1
                    color: ThemeManager.borderColor
                }

                CustomButton {
                    text: "æŸ¥çœ‹è¯¦æƒ…"
                    Layout.fillWidth: true
                    onClicked: {
                        globalState.toggleDropdownVisible();
                        todoDetailsDialog.openTodoDetails(todoItemDropdown.currentTodoData, todoItemDropdown.currentTodoIndex);
                    }
                }

                CustomButton {
                    text: "æ ‡è®°å®Œæˆ"
                    Layout.fillWidth: true
                    onClicked: {
                        todoManager.markAsDone(todoItemDropdown.currentTodoIndex);
                        globalState.toggleDropdownVisible();
                    }
                }

                CustomButton {
                    text: "åˆ é™¤ä»»åŠ¡"
                    Layout.fillWidth: true
                    onClicked: {
                        todoManager.removeTodo(todoItemDropdown.currentTodoIndex);
                        globalState.toggleDropdownVisible();
                    }
                }
            }
        }
    }
}
