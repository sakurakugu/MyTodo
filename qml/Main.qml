/**
 * @file Main.qml
 * @brief åº”ç”¨ç¨‹åºä¸»çª—å£
 *
 * è¯¥æ–‡ä»¶å®šä¹‰äº†MyTodoåº”ç”¨ç¨‹åºçš„ä¸»çª—å£ç•Œé¢ï¼ŒåŒ…æ‹¬ï¼š
 * - æ— è¾¹æ¡†çª—å£è®¾è®¡
 * - æ¡Œé¢å°ç»„ä»¶æ¨¡å¼æ”¯æŒ
 * - æ·±è‰²/æµ…è‰²ä¸»é¢˜åˆ‡æ¢
 * - çª—å£æ‹–æ‹½å’Œè°ƒæ•´å¤§å°åŠŸèƒ½
 * - é¡µé¢å¯¼èˆªå’ŒçŠ¶æ€ç®¡ç†
 *
 * @author Sakurakugu
 * @date 2025-08-16 20:05:55(UTC+8) å‘¨å…­
 * @version 2025-08-23 21:09:00(UTC+8) å‘¨å…­
 */

import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Window
import "components"

/**
 * @brief åº”ç”¨ç¨‹åºä¸»çª—å£
 *
 * ä¸»çª—å£é‡‡ç”¨æ— è¾¹æ¡†è®¾è®¡ï¼Œæ”¯æŒæ¡Œé¢å°ç»„ä»¶æ¨¡å¼å’Œæ™®é€šçª—å£æ¨¡å¼ã€‚
 * æä¾›äº†å®Œæ•´çš„ä¸»é¢˜ç³»ç»Ÿå’Œå“åº”å¼å¸ƒå±€ã€‚
 */
Window {
    id: root
    width: 640
    height: 480
    minimumWidth: 640
    minimumHeight: 480
    visible: true
    title: qsTr("æˆ‘çš„å¾…åŠ")

    // èƒŒæ™¯é€æ˜åº¦è®¾ç½® - å¿…é¡»ä¿æŒé€æ˜ï¼Œå¦åˆ™åœ¨Windowsä¸‹ä¼šå‡ºç°é»‘è‰²èƒŒæ™¯é—®é¢˜
    color: "transparent"

    // çª—å£æ ‡å¿—è®¾ç½® - FramelessWindowHintå¿…é¡»å§‹ç»ˆå­˜åœ¨ï¼Œå¦åˆ™Windowsä¸‹ä¼šå‡ºç°èƒŒæ™¯å˜é»‘ä¸”æ— æ³•æ¢å¤çš„é—®é¢˜
    // TODO: Qt.WindowStaysOnTopHint åªåœ¨ Debug æ¨¡å¼ä¸‹å¯ç”¨
    flags: Qt.FramelessWindowHint | (isDesktopWidget ? Qt.Tool : Qt.Window) | Qt.WindowStaysOnTopHint

    // æ˜¾ç¤ºæ¨¡å¼æ§åˆ¶å±æ€§
    property bool isDesktopWidget: globalState.isDesktopWidget  ///< æ˜¯å¦ä¸ºæ¡Œé¢å°ç»„ä»¶æ¨¡å¼
    property bool isShowTodos: globalState.isShowTodos          ///< æ˜¯å¦æ˜¾ç¤ºæ‰€æœ‰ä»»åŠ¡ï¼ˆå°ç»„ä»¶æ¨¡å¼ä¸‹ï¼‰
    property bool isDarkMode: setting.get("setting/isDarkMode", false)           ///< æ·±è‰²æ¨¡å¼å¼€å…³ï¼Œä»é…ç½®æ–‡ä»¶è¯»å–
    property bool preventDragging: setting.get("setting/preventDragging", false) ///< æ˜¯å¦ç¦æ­¢çª—å£æ‹–æ‹½ï¼Œä»é…ç½®æ–‡ä»¶è¯»å–

    FontLoader {
        id: iconFont
        source: "qrc:/qt/qml/MyTodo/image/font_icon/iconfont.ttf"
    }

    // ä¸»é¢˜ç®¡ç†å™¨
    ThemeManager {
        id: theme
        isDarkMode: root.isDarkMode
    }

    /**
     * @brief ä¸»çª—å£èƒŒæ™¯å®¹å™¨
     *
     * ä¸ºæ•´ä¸ªçª—å£æä¾›åœ†è§’èƒŒæ™¯æ•ˆæœï¼ŒåŒ…è£¹æ‰€æœ‰å†…å®¹ã€‚
     */
    Rectangle {
        id: mainBackground
        visible: !root.isDesktopWidget
        anchors.fill: parent
        anchors.margins: 0
        color: theme.backgroundColor
        radius: 5                        ///< çª—å£åœ†è§’åŠå¾„
        border.color: theme.borderColor
        border.width: 1
        z: -1000                        ///< ç¡®ä¿èƒŒæ™¯åœ¨æ‰€æœ‰å†…å®¹ä¸‹æ–¹
    }

    /**
     * @brief çª—å£å°ºå¯¸åŒæ­¥è¿æ¥
     *
     * ç›‘å¬C++ç«¯globalStateå¯¹è±¡çš„å°ºå¯¸å˜åŒ–ï¼Œ
     * ç¡®ä¿QMLçª—å£ä¸C++çª—å£å°ºå¯¸ä¿æŒåŒæ­¥ã€‚
     * ä»…åœ¨æ¡Œé¢å°ç»„ä»¶æ¨¡å¼ä¸‹æœ‰æ•ˆã€‚
     */
    Connections {
        target: globalState

        /// å¤„ç†çª—å£å®½åº¦å˜åŒ–
        function onWidthChanged(width) {
            root.width = width;
        }

        /// å¤„ç†çª—å£é«˜åº¦å˜åŒ–
        function onHeightChanged(height) {
            root.height = height;
        }
    }

    /**
     * @brief åº”ç”¨ç¨‹åºæ ‡é¢˜æ 
     *
     * è‡ªå®šä¹‰æ ‡é¢˜æ ï¼Œæ”¯æŒçª—å£æ‹–æ‹½åŠŸèƒ½ã€‚
     * åœ¨æ¡Œé¢å°ç»„ä»¶æ¨¡å¼å’Œæ™®é€šçª—å£æ¨¡å¼ä¸‹æœ‰ä¸åŒçš„æ ·å¼ã€‚
     */
    Rectangle {
        id: titleBar
        anchors.top: parent.top
        width: root.isDesktopWidget ? 400 : parent.width     ///< å°ç»„ä»¶æ¨¡å¼å›ºå®šå®½åº¦ï¼Œæ™®é€šæ¨¡å¼å¡«å……çˆ¶å®¹å™¨
        height: root.isDesktopWidget ? 35 : 48               ///< å°ç»„ä»¶æ¨¡å¼è¾ƒå°é«˜åº¦ï¼Œæ™®é€šæ¨¡å¼è¾ƒå¤§é«˜åº¦
        color: theme.primaryColor                            ///< ä½¿ç”¨ä¸»é¢˜ä¸»è‰²è°ƒ
        border.color: theme.borderColor                      ///< å°ç»„ä»¶æ¨¡å¼æ˜¾ç¤ºè¾¹æ¡†
        border.width: 0                                     ///< è¾¹æ¡†å®½åº¦
        topLeftRadius: 5                                     ///< å·¦ä¸Šè§’åœ†è§’
        topRightRadius: 5                                    ///< å³ä¸Šè§’åœ†è§’
        bottomLeftRadius: root.isDesktopWidget ? 5 : 0       ///< å·¦ä¸‹è§’æ— åœ†è§’
        bottomRightRadius: root.isDesktopWidget ? 5 : 0      ///< å³ä¸‹è§’æ— åœ†è§’

        /**
         * @brief çª—å£æ‹–æ‹½å¤„ç†åŒºåŸŸ
         *
         * å¤„ç†æ ‡é¢˜æ çš„é¼ æ ‡æ‹–æ‹½äº‹ä»¶ï¼Œå®ç°çª—å£ç§»åŠ¨åŠŸèƒ½ã€‚
         * æ”¯æŒé˜²æ‹–æ‹½è®¾ç½®å’Œä¸åŒæ¨¡å¼ä¸‹çš„æ‹–æ‹½æ§åˆ¶ã€‚
         */
        MouseArea {
            anchors.fill: parent
            property point clickPos: Qt.point(0, 0)  ///< è®°å½•é¼ æ ‡æŒ‰ä¸‹æ—¶çš„ä½ç½®
            property bool wasMaximized: false        ///< è®°å½•æ‹–æ‹½å¼€å§‹æ—¶æ˜¯å¦ä¸ºæœ€å¤§åŒ–çŠ¶æ€

            /// é¼ æ ‡æŒ‰ä¸‹äº‹ä»¶å¤„ç†
            onPressed: function (mouse) {
                clickPos = Qt.point(mouse.x, mouse.y);
                wasMaximized = (root.visibility === Window.Maximized);
            }

            /// é¼ æ ‡ç§»åŠ¨äº‹ä»¶å¤„ç† - å®ç°çª—å£æ‹–æ‹½
            onPositionChanged: function (mouse) {
                // åªæœ‰åœ¨éå°ç»„ä»¶æ¨¡å¼æˆ–å°ç»„ä»¶æ¨¡å¼ä½†æœªå¯ç”¨é˜²æ­¢æ‹–åŠ¨æ—¶æ‰å…è®¸æ‹–åŠ¨
                if (pressed && ((!root.isDesktopWidget) || (root.isDesktopWidget && !root.preventDragging))) {
                    if (wasMaximized) {
                        // å¦‚æœæ˜¯ä»æœ€å¤§åŒ–çŠ¶æ€å¼€å§‹æ‹–æ‹½ï¼Œéœ€è¦ç‰¹æ®Šå¤„ç†
                        var mouseRatioX = clickPos.x / titleBar.width;  // è®¡ç®—é¼ æ ‡åœ¨æ ‡é¢˜æ ä¸­çš„ç›¸å¯¹ä½ç½®
                        root.showNormal();
                        // æ ¹æ®é¼ æ ‡ç›¸å¯¹ä½ç½®è°ƒæ•´çª—å£ä½ç½®ï¼Œä½¿é¼ æ ‡ä¿æŒåœ¨ç›¸åŒçš„ç›¸å¯¹ä½ç½®
                        var globalMousePos = mapToGlobal(mouse.x, mouse.y);
                        root.x = globalMousePos.x - (root.width * mouseRatioX);
                        root.y = globalMousePos.y - mouse.y;
                        // æ›´æ–°ç‚¹å‡»ä½ç½®ä¸ºæ–°çª—å£å¤§å°ä¸‹çš„ç›¸å¯¹ä½ç½®
                        clickPos = Qt.point(root.width * mouseRatioX, mouse.y);
                        wasMaximized = false;
                    } else {
                        // æ™®é€šæ‹–æ‹½é€»è¾‘
                        var delta = Qt.point(mouse.x - clickPos.x, mouse.y - clickPos.y);
                        root.x += delta.x;
                        root.y += delta.y;
                    }
                }
            }
            z: -1 ///< ç¡®ä¿æ­¤MouseAreaåœ¨å…¶ä»–æ§ä»¶ä¸‹å±‚ï¼Œä¸å½±å“å…¶ä»–æ§ä»¶çš„ç‚¹å‡»äº‹ä»¶
        }

        /**
         * @brief æ ‡é¢˜æ å†…å®¹å¸ƒå±€
         *
         * åŒ…å«ç”¨æˆ·ä¿¡æ¯åŒºåŸŸå’Œæ§åˆ¶æŒ‰é’®ç»„ï¼Œ
         * æ ¹æ®ä¸åŒæ¨¡å¼æ˜¾ç¤ºä¸åŒçš„å†…å®¹ã€‚
         */
        RowLayout {
            anchors.fill: parent
            anchors.leftMargin: root.isDesktopWidget ? 5 : 10   ///< å·¦è¾¹è·
            anchors.rightMargin: root.isDesktopWidget ? 5 : 10  ///< å³è¾¹è·

            /**
             * @brief é¡µé¢å¯¼èˆªåŒºåŸŸ
             *
             * å½“åœ¨å­é¡µé¢æ—¶æ˜¾ç¤ºè¿”å›æŒ‰é’®å’Œé¡µé¢æ ‡é¢˜
             */
            RowLayout {
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: 0
                visible: !root.isDesktopWidget && stackView.depth > 1  ///< ä»…åœ¨éå°ç»„ä»¶æ¨¡å¼ä¸”æœ‰å­é¡µé¢æ—¶æ˜¾ç¤º

                ToolButton {
                    font.family: iconFont.name
                    text: "\ue8fa"
                    font.bold: true
                    font.pixelSize: 18
                    onClicked: stackView.pop()
                }

                Label {
                    text: stackView.currentItem && stackView.currentItem.objectName === "settingPage" ? qsTr("è®¾ç½®") : ""
                    font.bold: true                     ///< ç²—ä½“å­—
                    font.pixelSize: 16
                    color: theme.titleBarTextColor
                }
            }

            /**
             * @brief ç”¨æˆ·ä¿¡æ¯æ˜¾ç¤ºåŒºåŸŸ
             *
             * ä»…åœ¨æ™®é€šçª—å£æ¨¡å¼ä¸‹æ˜¾ç¤ºï¼ŒåŒ…å«ç”¨æˆ·å¤´åƒå’Œç”¨æˆ·åã€‚
             * ç‚¹å‡»å¯å¼¹å‡ºç”¨æˆ·èœå•ã€‚
             */
            RowLayout {
                Layout.fillWidth: true
                Layout.fillHeight: true
                visible: !root.isDesktopWidget && stackView.depth <= 1  ///< ä»…åœ¨éå°ç»„ä»¶æ¨¡å¼ä¸”åœ¨ä¸»é¡µé¢æ—¶æ˜¾ç¤º

                /**
                 * @brief ç”¨æˆ·èµ„æ–™ç‚¹å‡»åŒºåŸŸ
                 *
                 * å¤„ç†ç”¨æˆ·å¤´åƒå’Œä¿¡æ¯çš„ç‚¹å‡»äº‹ä»¶ï¼Œå¼¹å‡ºç”¨æˆ·èœå•ã€‚
                 */
                MouseArea {
                    id: userProfileMouseArea
                    Layout.preferredWidth: userProfileContent.width
                    Layout.preferredHeight: userProfileContent.height
                    Layout.alignment: Qt.AlignVCenter
                    z: 10  // ç¡®ä¿åœ¨æ‹–æ‹½MouseAreaä¹‹ä¸Š

                    /// ç‚¹å‡»å¼¹å‡ºç”¨æˆ·èœå•
                    onClicked: {
                        // è®¡ç®—èœå•ä½ç½®ï¼Œå›ºå®šåœ¨ç”¨æˆ·å¤´åƒä¸‹æ–¹
                        var pos = mapToItem(null, 0, height + 9);
                        topMenu.popup(pos.x, pos.y);
                    }

                    RowLayout {
                        id: userProfileContent
                        spacing: 10

                        /**
                         * @brief ç”¨æˆ·å¤´åƒæ˜¾ç¤º
                         *
                         * åœ†å½¢å¤´åƒå®¹å™¨ï¼Œæ˜¾ç¤ºé»˜è®¤ç”¨æˆ·å›¾æ ‡ã€‚
                         */
                        Rectangle {
                            width: 30
                            height: 30
                            radius: 15                               ///< åœ†å½¢å¤´åƒ
                            color: theme.secondaryBackgroundColor    ///< ä½¿ç”¨ä¸»é¢˜æ¬¡è¦èƒŒæ™¯è‰²
                            Layout.alignment: Qt.AlignVCenter        ///< å‚ç›´å±…ä¸­å¯¹é½

                            /// å¤´åƒå›¾æ ‡
                            Text {
                                anchors.centerIn: parent
                                text: "ğŸ‘¤"                      ///< é»˜è®¤ç”¨æˆ·å›¾æ ‡
                                font.pixelSize: 18
                            }
                        }

                        /**
                         * @brief ç”¨æˆ·åæ˜¾ç¤º
                         *
                         * æ˜¾ç¤ºå½“å‰ç™»å½•ç”¨æˆ·çš„ç”¨æˆ·åï¼Œæœªç™»å½•æ—¶æ˜¾ç¤ºæç¤ºæ–‡æœ¬ã€‚
                         */
                        Text {
                            text: userAuth.username !== "" ? userAuth.username : qsTr("æœªç™»å½•")
                            color: theme.titleBarTextColor      ///< ä½¿ç”¨ä¸»é¢˜æ–‡æœ¬é¢œè‰²
                            font.bold: true                     ///< ç²—ä½“å­—
                            font.pixelSize: 16                  ///< å­—ä½“å¤§å°
                            Layout.alignment: Qt.AlignVCenter   ///< å‚ç›´å±…ä¸­å¯¹é½
                            horizontalAlignment: Text.AlignLeft ///< æ°´å¹³å·¦å¯¹é½
                        }
                    }
                }

                /// æ ‡é¢˜æ å‰©ä½™ç©ºé—´å¡«å……
                Item {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                }
            }

            /// è®¾ç½®æŒ‰é’®
            IconButton {
                text: "\ue90f"                                       ///< èœå•å›¾æ ‡
                onClicked: globalState.toggleSettingsVisible()  ///< åˆ‡æ¢è®¾ç½®ç•Œé¢æ˜¾ç¤º
                visible: root.isDesktopWidget
                textColor: theme.titleBarTextColor
                fontSize: 16
                isDarkMode: root.isDarkMode
            }

            /// å¾…åŠçŠ¶æ€æŒ‡ç¤ºå™¨
            Text {
                id: todoStatusIndicator
                visible: root.isDesktopWidget
                Layout.alignment: Qt.AlignVCenter
                color: theme.titleBarTextColor
                font.pixelSize: 14
                font.bold: true
                
                // TODO: æ–°å¢æˆ–åˆ é™¤å¾…åŠåï¼Œè¿™é‡Œçš„æ–‡å­—æ²¡æœ‰æ›´æ”¹ï¼ˆæ˜¾ç¤ºçš„æ˜¯å½“å‰åˆ†ç±»ä¸‹çš„å¾…åŠæ•°é‡ï¼‰
                property int todoCount: todoManager.rowCount()
                property bool isHovered: false
                
                text: {
                    if (isHovered) {
                        return todoCount > 0 ? todoCount + "ä¸ªå¾…åŠ" : "æ²¡æœ‰å¾…åŠ"
                    } else {
                        return todoCount > 0 ? todoCount + "ä¸ªå¾…åŠ" : "æˆ‘çš„å¾…åŠ"
                    }
                }
                
                MouseArea {
                    anchors.fill: parent
                    hoverEnabled: true
                    
                    onEntered: {
                        parent.isHovered = true
                    }
                    
                    onExited: {
                        parent.isHovered = false
                    }
                }
            }

            /// æ ‡é¢˜æ å‰©ä½™ç©ºé—´å¡«å……
            Item {
                visible: root.isDesktopWidget
                Layout.fillWidth: true
                Layout.fillHeight: true
            }

            /// ä»»åŠ¡åˆ—è¡¨å±•å¼€/æ”¶èµ·æŒ‰é’®
            IconButton {
                text: isShowTodos ? "\ue667" : "\ue669"        ///< æ ¹æ®çŠ¶æ€æ˜¾ç¤ºç®­å¤´
                onClicked: globalState.toggleTodosVisible()     ///< åˆ‡æ¢ä»»åŠ¡åˆ—è¡¨æ˜¾ç¤º
                visible: root.isDesktopWidget
                textColor: theme.titleBarTextColor
                fontSize: 16
                isDarkMode: root.isDarkMode
            }

            /// æ·»åŠ ä»»åŠ¡æŒ‰é’®
            IconButton {
                text: "\ue903"                                 ///< åŠ å·å›¾æ ‡
                onClicked: globalState.toggleAddTaskVisible()   ///< åˆ‡æ¢æ·»åŠ ä»»åŠ¡ç•Œé¢æ˜¾ç¤º
                visible: root.isDesktopWidget
                textColor: theme.titleBarTextColor
                fontSize: 16
                isDarkMode: root.isDarkMode
            }

            /// æ™®é€šæ¨¡å¼å’Œå°ç»„ä»¶æ¨¡å¼åˆ‡æ¢æŒ‰é’®
            IconButton {
                text: root.isDesktopWidget ? "\ue620" : "\ue61f"
                /// é¼ æ ‡æŒ‰ä¸‹äº‹ä»¶å¤„ç†
                onClicked: {
                    if (root.isDesktopWidget) {
                        globalState.toggleWidgetMode();
                        globalState.isShowTodos = true;
                    } else {
                        globalState.toggleWidgetMode();
                    }
                }
                textColor: theme.titleBarTextColor
                fontSize: 18
                isDarkMode: root.isDarkMode
            }

            // æœ€å°åŒ–æŒ‰é’®
            IconButton {
                text: "\ue65a"
                onClicked: root.showMinimized()
                textColor: theme.titleBarTextColor
                visible: !root.isDesktopWidget
                fontSize: 16
                isDarkMode: root.isDarkMode
            }

            // æœ€å¤§åŒ–/æ¢å¤æŒ‰é’®
            IconButton {
                text: root.visibility === Window.Maximized ? "\ue600" : "\ue65b"
                visible: !root.isDesktopWidget
                onClicked: {
                    if (root.visibility === Window.Maximized) {
                        root.showNormal();
                    } else {
                        root.showMaximized();
                    }
                }
                textColor: theme.titleBarTextColor
                fontSize: 16
                isDarkMode: root.isDarkMode
            }

            // å…³é—­æŒ‰é’®
            IconButton {
                text: "\ue8d1"
                onClicked: root.close()
                visible: !root.isDesktopWidget
                fontSize: 16
                textColor: theme.titleBarTextColor
                isDarkMode: root.isDarkMode
            }
        }
    }

    // è¾¹æ¡†è°ƒæ•´å¤§å°åŒºåŸŸ
    property int resizeBorderWidth: 5     ///< è¾¹æ¡†è°ƒæ•´å¤§å°çš„è¾¹è·å®½åº¦
    // å·¦è¾¹æ¡†
    MouseArea {
        id: leftResizeArea
        width: resizeBorderWidth
        anchors.left: parent.left
        anchors.top: parent.top
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeHorCursor
        z: 2000

        property string edge: "left"

        onPressed: {
            root.startSystemResize(Qt.LeftEdge);
        }
    }

    // å³è¾¹æ¡†
    MouseArea {
        id: rightResizeArea
        width: resizeBorderWidth
        anchors.right: parent.right
        anchors.top: parent.top
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeHorCursor
        z: 2000

        property string edge: "right"

        onPressed: {
            root.startSystemResize(Qt.RightEdge);
        }
    }

    // ä¸Šè¾¹æ¡†
    MouseArea {
        id: topResizeArea
        height: resizeBorderWidth
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.right: parent.right
        visible: !isDesktopWidget
        cursorShape: Qt.SizeVerCursor
        z: 2000

        property string edge: "top"

        onPressed: {
            root.startSystemResize(Qt.TopEdge);
        }
    }

    // ä¸‹è¾¹æ¡†
    MouseArea {
        id: bottomResizeArea
        height: resizeBorderWidth
        anchors.bottom: parent.bottom
        anchors.left: parent.left
        anchors.right: parent.right
        visible: !isDesktopWidget
        cursorShape: Qt.SizeVerCursor
        z: 2000

        property string edge: "bottom"

        onPressed: {
            root.startSystemResize(Qt.BottomEdge);
        }
    }

    // å·¦ä¸Šè§’
    MouseArea {
        id: topLeftResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.left: parent.left
        anchors.top: parent.top
        visible: !isDesktopWidget
        cursorShape: Qt.SizeFDiagCursor
        z: 2000

        property string edge: "topleft"

        onPressed: {
            root.startSystemResize(Qt.TopEdge | Qt.LeftEdge);
        }
    }

    // å³ä¸Šè§’
    MouseArea {
        id: topRightResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.right: parent.right
        anchors.top: parent.top
        visible: !isDesktopWidget
        cursorShape: Qt.SizeBDiagCursor
        z: 2000

        property string edge: "topright"

        onPressed: {
            root.startSystemResize(Qt.TopEdge | Qt.RightEdge);
        }
    }

    // å·¦ä¸‹è§’
    MouseArea {
        id: bottomLeftResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.left: parent.left
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeBDiagCursor
        z: 2000

        property string edge: "bottomleft"

        onPressed: {
            root.startSystemResize(Qt.BottomEdge | Qt.LeftEdge);
        }
    }

    // å³ä¸‹è§’
    MouseArea {
        id: bottomRightResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeFDiagCursor
        z: 2000

        property string edge: "bottomright"

        onPressed: {
            root.startSystemResize(Qt.BottomEdge | Qt.RightEdge);
        }
    }

    // å¯¼èˆªæ ˆï¼Œè¿›è¡Œé¡µé¢ç®¡ç†
    StackView {
        id: stackView
        anchors.top: titleBar.bottom
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        anchors.margins: 0
        z: 1000
        focus: true
        visible: !root.isDesktopWidget && depth > 0 // å°çª—å£æ¨¡å¼æ—¶éšè—ä¸»é¡µé¢
        initialItem: mainPageComponent
        clip: true  ///< è£å‰ªå†…å®¹ä»¥é…åˆçª—å£åœ†è§’æ•ˆæœ
    }

    // å°ç»„ä»¶æ¨¡å¼ç»„ä»¶
    WidgetMode {
        id: widgetModeComponent
        anchors.fill: parent
        isDesktopWidget: root.isDesktopWidget
        isShowTodos: root.isShowTodos
        isDarkMode: root.isDarkMode
        preventDragging: root.preventDragging
        visible: isDesktopWidget

        // è¿æ¥ä¿¡å·
        onPreventDraggingToggled: function (value) {
            root.preventDragging = value;
        }

        onDarkModeToggled: function (value) {
            root.isDarkMode = value;
        }
    }

    MainPage {
        id: mainPageComponent
        isDarkMode: root.isDarkMode
        isDesktopWidget: root.isDesktopWidget
        rootWindow: root
    }

    // é¡¶éƒ¨ç”¨æˆ·èœå•ï¼ˆä»å¤´åƒå¤„ç‚¹å‡»å¼¹å‡ºï¼‰
    Menu {
        id: topMenu
        width: 200
        height: implicitHeight
        z: 10000  // ç¡®ä¿èœå•æ˜¾ç¤ºåœ¨æœ€ä¸Šå±‚

        background: Rectangle {
            color: theme.backgroundColor
            border.color: theme.borderColor
            border.width: 1
            radius: 4
        }

        MenuItem {
            text: todoManager.isLoggedIn ? qsTr("é€€å‡ºç™»å½•") : qsTr("ç™»å½•")
            contentItem: Row {
                spacing: 8
                Text {
                    text: "\ue981"
                    font.family: iconFont.name
                    color: theme.textColor
                    font.pixelSize: 18
                    anchors.verticalCenter: parent.verticalCenter
                }
                Text {
                    text: parent.parent.text
                    color: theme.textColor
                    // font: parent.parent.font
                    font.pixelSize: 18
                    anchors.verticalCenter: parent.verticalCenter
                }
            }
            onTriggered: {
                if (todoManager.isLoggedIn) {
                    loginStatusDialogs.showLogoutConfirm();
                } else {
                    loginStatusDialogs.showLoginDialog();
                }
            }
        }

        MenuItem {
            text: qsTr("è®¾ç½®")
            contentItem: Row {
                spacing: 8
                Text {
                    text: "\ue913"
                    font.family: iconFont.name
                    color: theme.textColor
                    font.pixelSize: 18
                    anchors.verticalCenter: parent.verticalCenter
                }
                Text {
                    text: parent.parent.text
                    color: theme.textColor
                    font.pixelSize: 18
                    anchors.verticalCenter: parent.verticalCenter
                }
            }
            onTriggered: {
                stackView.push(Qt.resolvedUrl("Setting.qml"), {
                    isDarkMode: root.isDarkMode,
                    preventDragging: root.preventDragging,
                    rootWindow: root
                });
            }
        }

        MenuItem {
            id: darkModeItem
            contentItem: RowLayout {
                spacing: 12
                Row {
                    spacing: 8
                    Text {
                        text: root.isDarkMode ? "\ue668" : "\ue62e"
                        font.family: iconFont.name
                        color: theme.textColor
                        font.pixelSize: 18
                        anchors.verticalCenter: parent.verticalCenter
                    }
                    Text {
                        text: qsTr("æ·±è‰²æ¨¡å¼")
                        color: theme.textColor
                        font.pixelSize: 18
                        anchors.verticalCenter: parent.verticalCenter
                    }
                }
                Switch {
                    id: darkModeSwitch
                    leftPadding: 0
                    checked: root.isDarkMode
                    scale: 0.7

                    onCheckedChanged: {
                        root.isDarkMode = checked;
                        setting.save("setting/isDarkMode", checked);
                    }
                }
            }
        }

        MenuItem {
            id: onlineToggleItem
            contentItem: RowLayout {
                spacing: 12
                Row {
                    spacing: 8
                    Text {
                        text: "\ue8ef"
                        font.family: iconFont.name
                        color: theme.textColor
                        font.pixelSize: 18
                        anchors.verticalCenter: parent.verticalCenter
                    }
                    Text {
                        text: qsTr("è‡ªåŠ¨åŒæ­¥")
                        color: theme.textColor
                        font.pixelSize: 18
                        anchors.verticalCenter: parent.verticalCenter
                    }
                }
                Switch {
                    id: onlineSwitch
                    leftPadding: 0
                    checked: setting.get("setting/autoSync", false)
                    scale: 0.7

                    onCheckedChanged: {
                        if (checked && !todoManager.isLoggedIn) {
                            // å¦‚æœè¦å¼€å¯è‡ªåŠ¨åŒæ­¥ä½†æœªç™»å½•ï¼Œæ˜¾ç¤ºæç¤ºå¹¶é‡ç½®å¼€å…³
                            onlineSwitch.checked = false;
                            topMenu.close(); // å…³é—­èœå•
                            loginStatusDialogs.showLoginRequired();
                        } else {
                            setting.save("setting/autoSync", checked);
                        }
                    }
                }
            }
            // é˜»æ­¢ç‚¹å‡»æ•´è¡Œè§¦å‘é»˜è®¤åˆ‡æ¢
            onTriggered: {}
        }
    }

    // ç™»å½•ç›¸å…³å¯¹è¯æ¡†ç»„ä»¶
    LoginStatusDialogs {
        id: loginStatusDialogs
        isDarkMode: root.isDarkMode
    }
}
