import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Window

Window {
    id: root
    width: 640
    height: 480
    minimumWidth: 640
    minimumHeight: 480
    visible: true
    title: qsTr("æˆ‘çš„å¾…åŠ")
    color: "transparent" // è¦å§‹ç»ˆä¿æŒèƒŒæ™¯é€æ˜ï¼Œä¸ç„¶windowä¸‹å†æ¬¡å˜é€æ˜ä¼šå˜é»‘
    flags: Qt.FramelessWindowHint | (isDesktopWidget ? Qt.Tool : Qt.Window) // Qt.FramelessWindowHint è¦ä¸€ç›´ä¿æŒå­˜åœ¨ï¼Œä¸ç„¶windowsä¼šåœ¨æ²¡æœ‰å®ƒæ—¶èƒŒæ™¯å˜é»‘ï¼Œè€Œä¸”å˜ä¸å›æ¥......

    property int resizeBorderWidth: 5     // å®šä¹‰è¾¹æ¡†è°ƒæ•´å¤§å°çš„è¾¹è·
    property bool isDesktopWidget: mainWindow.isDesktopWidget  // æ˜¯å¦æ˜¯æ¡Œé¢å°ç»„ä»¶æ¨¡å¼
    property bool isShowTodos: mainWindow.isShowTodos       // æ˜¯å¦å±•ç¤ºæ‰€æœ‰ä»»åŠ¡ï¼Œåœ¨å°ç»„ä»¶æ¨¡å¼ä¸‹
    property bool isShowAddTask: mainWindow.isShowAddTask    // æ˜¯å¦å±•ç¤ºæ·»åŠ ä»»åŠ¡
    property bool isShowSetting: mainWindow.isShowSetting    // æ˜¯å¦å±•ç¤ºè®¾ç½®
    // property bool isDarkMode: settings.get("isDarkMode", false)
    property bool isDarkMode: false       // æ·»åŠ æ·±è‰²æ¨¡å¼å±æ€§
    property bool preventDragging: settings.get("preventDragging", false) // æ˜¯å¦é˜²æ­¢æ‹–åŠ¨

    // ä½¿ç”¨StackViewè¿›è¡Œé¡µé¢å¯¼èˆª

    // é¢œè‰²ä¸»é¢˜
    property color primaryColor: isDarkMode ? "#2c3e50" : "#4a86e8"
    property color backgroundColor: isDarkMode ? "#1e272e" : "white"
    property color secondaryBackgroundColor: isDarkMode ? "#2d3436" : "#f5f5f5"
    property color textColor: isDarkMode ? "#ecf0f1" : "black"
    property color borderColor: isDarkMode ? "#34495e" : "#cccccc"

    // ç›‘å¬mainWindowçš„å®½åº¦å’Œé«˜åº¦å˜åŒ–
    Connections {
        target: mainWindow
        function onWidthChanged() {
            root.width = mainWindow.width;
        }
        function onHeightChanged() {
            root.height = mainWindow.height;
        }
    }

    // å°ç»„ä»¶æ¨¡å¼çš„è®¾ç½®å¼¹å‡ºçª—å£
    Popup {
        id: settingsPopup
        y: 50
        width: 400
        height: 300

        // implicitHeight: Math.min(600, contentItem.implicitHeight)
        modal: false // éæ¨¡æ€ï¼Œå…è®¸åŒæ—¶æ‰“å¼€å¤šä¸ªå¼¹å‡ºçª—å£
        focus: true
        closePolicy: Popup.NoAutoClose
        visible: isDesktopWidget && isShowSetting

        contentItem: Rectangle {
            color: secondaryBackgroundColor
            border.color: borderColor
            border.width: 1
            radius: 5

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 10

                Label {
                    text: "è®¾ç½®"
                    font.bold: true
                    font.pixelSize: 16
                    color: textColor
                }

                // è®¾ç½®å†…å®¹
                CheckBox {
                    id: darkModeCheckBox
                    text: "æ·±è‰²æ¨¡å¼"
                    checked: isDarkMode
                    onCheckedChanged: {
                        isDarkMode = checked;
                        // ä¿å­˜è®¾ç½®åˆ°é…ç½®æ–‡ä»¶
                        settings.save("isDarkMode", isDarkMode);
                    }
                }

                CheckBox {
                    id: preventDraggingCheckBox
                    text: "é˜²æ­¢æ‹–åŠ¨çª—å£ï¼ˆå°çª—å£æ¨¡å¼ï¼‰"
                    checked: preventDragging
                    enabled: isDesktopWidget
                    onCheckedChanged: {
                        preventDragging = checked;
                        // ä¿å­˜è®¾ç½®åˆ°é…ç½®æ–‡ä»¶
                        settings.save("preventDragging", preventDragging);
                    }
                }

                CheckBox {
                    id: autoSyncCheckBox
                    text: "è‡ªåŠ¨åŒæ­¥"
                    checked: false
                    onCheckedChanged:
                    // TODO: ç»‘å®šè‡ªåŠ¨åŒæ­¥é€»è¾‘
                    {}
                }

                Item {
                    Layout.fillHeight: true
                } // å ä½

                Button {
                    text: "å…³é—­"
                    Layout.alignment: Qt.AlignRight
                    onClicked: settingsPopup.close()
                }
            }
        }
    }

    // å°ç»„ä»¶æ¨¡å¼çš„æ·»åŠ ä»»åŠ¡å¼¹å‡ºçª—å£
    Popup {
        id: addTaskPopup
        y: settingsPopup.visible ? settingsPopup.y + settingsPopup.height + 6 : 50
        width: 400
        height: 200
        // implicitHeight: Math.min(600, contentItem.implicitHeight)
        modal: false // éæ¨¡æ€ï¼Œå…è®¸åŒæ—¶æ‰“å¼€å¤šä¸ªå¼¹å‡ºçª—å£
        focus: true
        closePolicy: Popup.NoAutoClose
        visible: isDesktopWidget && isShowAddTask

        contentItem: Rectangle {
            color: secondaryBackgroundColor
            border.color: borderColor
            border.width: 1
            radius: 5

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 10

                Label {
                    text: "æ·»åŠ ä»»åŠ¡"
                    font.bold: true
                    font.pixelSize: 16
                }

                TextField {
                    id: newTaskField
                    Layout.fillWidth: true
                    placeholderText: "è¾“å…¥æ–°ä»»åŠ¡..."
                    onAccepted: {
                        if (text.trim() !== "") {
                            todoModel.addTodo(text.trim());
                            text = "";
                            addTaskPopup.close();
                        }
                    }
                }

                ComboBox {
                    id: taskCategory
                    Layout.fillWidth: true
                    model: ["å·¥ä½œ", "å­¦ä¹ ", "ç”Ÿæ´»", "å…¶ä»–"]
                    currentIndex: 0
                }

                RowLayout {
                    Layout.fillWidth: true
                    Layout.alignment: Qt.AlignRight

                    Button {
                        text: "å–æ¶ˆ"
                        onClicked: addTaskPopup.close()
                    }

                    Button {
                        text: "æ·»åŠ "
                        onClicked: {
                            if (newTaskField.text.trim() !== "") {
                                todoModel.addTodo(newTaskField.text.trim());
                                newTaskField.text = "";
                                addTaskPopup.close();
                            }
                        }
                    }
                }
            }
        }
    }

    // å°ç»„ä»¶æ¨¡å¼çš„ä¸»å†…å®¹åŒºå¼¹å‡ºçª—å£
    Popup {
        id: mainContentPopup

        // æ ¹æ®å…¶ä»–å¼¹å‡ºçª—å£çš„å¯è§æ€§åŠ¨æ€è®¡ç®—ä½ç½®
        y: {
            if (addTaskPopup.visible) {
                return addTaskPopup.y + addTaskPopup.height + 6;
            } else if (settingsPopup.visible) {
                return settingsPopup.y + settingsPopup.height + 6;
            }
            return 50;
        }

        width: 400
        height: 200
        modal: false // éæ¨¡æ€ï¼Œå…è®¸åŒæ—¶æ‰“å¼€å¤šä¸ªå¼¹å‡ºçª—å£
        focus: true
        closePolicy: Popup.NoAutoClose
        visible: isDesktopWidget && isShowTodos // åœ¨å°ç»„ä»¶æ¨¡å¼ä¸‹ä¸”éœ€è¦æ˜¾ç¤ºæ‰€æœ‰ä»»åŠ¡æ—¶æ˜¾ç¤º

        contentItem: Rectangle {
            color: secondaryBackgroundColor
            border.color: borderColor
            border.width: 1
            radius: 5

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 10

                Label {
                    text: "å¾…åŠä»»åŠ¡"
                    font.bold: true
                    font.pixelSize: 16
                }

                // å¾…åŠåˆ—è¡¨
                ListView {
                    id: todoListPopupView
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    clip: true
                    model: todoModel

                    delegate: Rectangle {
                        width: popupTodoListView.width
                        height: 40
                        color: index % 2 === 0 ? secondaryBackgroundColor : backgroundColor

                        RowLayout {
                            anchors.fill: parent
                            anchors.margins: 5
                            spacing: 5

                            // å¾…åŠçŠ¶æ€æŒ‡ç¤ºå™¨
                            Rectangle {
                                width: 12
                                height: 12
                                radius: 6
                                color: model.status === "done" ? "#4caf50" : model.urgency === "high" ? "#f44336" : model.urgency === "medium" ? "#ff9800" : "#8bc34a"

                                MouseArea {
                                    anchors.fill: parent
                                    onClicked: {
                                        todoModel.markAsDone(index);
                                        mouse.accepted = true;
                                    }
                                }
                            }

                            // å¾…åŠæ ‡é¢˜
                            Label {
                                text: model.title
                                font.strikeout: model.status === "done"
                                font.pixelSize: 12
                                Layout.fillWidth: true
                                elide: Text.ElideRight
                            }

                            // åˆ é™¤æŒ‰é’®
                            Rectangle {
                                width: 20
                                height: 20
                                color: "transparent"

                                Text {
                                    anchors.centerIn: parent
                                    text: "Ã—"
                                    font.pixelSize: 14
                                    color: deletePopupMouseArea.pressed ? "darkgray" : "gray"
                                }

                                MouseArea {
                                    id: deletePopupMouseArea
                                    anchors.fill: parent
                                    onClicked: {
                                        todoModel.removeTodo(index);
                                    }
                                }
                            }
                        }
                    }

                    // ä¸ºå¼¹å‡ºåˆ—è¡¨æ·»åŠ æ»šåŠ¨æ¡
                    ScrollBar.vertical: ScrollBar {}
                }
            }
        }
    }

    // æ ‡é¢˜æ 
    Rectangle {
        id: titleBar
        anchors.top: parent.top
        width: parent.width
        height: isDesktopWidget ? 40 : 45
        color: primaryColor
        border.color: isDesktopWidget ? borderColor : "transparent"
        border.width: isDesktopWidget ? 1 : 0
        radius: isDesktopWidget ? 5 : 0

        // å…è®¸æŒ‰ä½æ ‡é¢˜æ æ‹–åŠ¨çª—å£
        MouseArea {
            anchors.fill: parent
            property point clickPos: "0,0"

            onPressed: {
                clickPos = Qt.point(mouse.x, mouse.y);
            }

            onPositionChanged: {
                // åªæœ‰åœ¨éå°ç»„ä»¶æ¨¡å¼æˆ–å°ç»„ä»¶æ¨¡å¼ä½†æœªå¯ç”¨é˜²æ­¢æ‹–åŠ¨æ—¶æ‰å…è®¸æ‹–åŠ¨
                if (pressed && (!isDesktopWidget || (isDesktopWidget && !preventDragging))) {
                    var delta = Qt.point(mouse.x - clickPos.x, mouse.y - clickPos.y);
                    root.x += delta.x;
                    root.y += delta.y;
                }
            }
            z: -1 // ç¡®ä¿æ­¤MouseAreaåœ¨å…¶ä»–æ§ä»¶ä¸‹å±‚ï¼Œä¸å½±å“å…¶ä»–æ§ä»¶çš„ç‚¹å‡»äº‹ä»¶
        }

        RowLayout {
            anchors.fill: parent
            anchors.leftMargin: 10
            anchors.rightMargin: 10

            // ç”¨æˆ·å¤´åƒå’Œä¿¡æ¯
            RowLayout {
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: 10
                visible: !isDesktopWidget

                // ç”¨æˆ·å¤´åƒå’Œä¿¡æ¯åŒºåŸŸ - åªæœ‰è¿™éƒ¨åˆ†ç‚¹å‡»æ—¶å¼¹å‡ºèœå•
                MouseArea {
                    id: userProfileMouseArea
                    Layout.preferredWidth: childrenRect.width
                    Layout.fillHeight: true
                    onClicked: {
                        // è®¡ç®—èœå•ä½ç½®ï¼Œå›ºå®šåœ¨ç”¨æˆ·å¤´åƒä¸‹æ–¹
                        var pos = mapToItem(null, 0, height);
                        topMenu.popup(pos.x, pos.y);
                    }

                    RowLayout {
                        spacing: 10

                        // ç”¨æˆ·å¤´åƒ
                        Rectangle {
                            width: 30
                            height: 30
                            radius: 15
                            color: "lightgray"
                            Layout.alignment: Qt.AlignVCenter  // å‚ç›´å±…ä¸­å¯¹é½

                            Text {
                                anchors.centerIn: parent
                                text: "ğŸ‘¤"
                                font.pixelSize: 18
                            }
                        }

                        // ç”¨æˆ·å
                        Text {
                            text: todoModel.getUsername() !== "" ? todoModel.getUsername() : "æœªç™»å½•"
                            color: "white"
                            font.bold: true
                            font.pixelSize: 14
                            Layout.alignment: Qt.AlignVCenter  // å‚ç›´å±…ä¸­å¯¹é½
                            horizontalAlignment: Text.AlignLeft  // æ°´å¹³å·¦å¯¹é½
                        }
                    }
                }

                // æ ‡é¢˜æ å‰©ä½™ç©ºé—´
                Item {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                }
            }

            MouseArea {
                id: settingsMouseArea
                Layout.preferredWidth: 40
                Layout.preferredHeight: 40
                Layout.alignment: Qt.AlignLeft | Qt.AlignTop
                onClicked: settingsPopup.visible ? settingsPopup.close() : settingsPopup.open()
                visible: isDesktopWidget

                Text {
                    anchors.centerIn: parent
                    text: "â˜°"
                    color: "white"
                    font.pixelSize: 20
                }
            }

            Button {
                text: isShowTodos ? "^" : "v"
                onClicked: mainWindow.toggleTodosVisible()
                visible: isDesktopWidget
                flat: true
                implicitWidth: 30
                background: Rectangle {
                    color: "transparent"
                }
            }

            Button {
                text: isDesktopWidget ? "å°" : "å¤§"
                onClicked: {
                    if (isDesktopWidget) {
                        mainWindow.toggleWidgetMode();
                        mainWindow.setIsShowTodos(true);
                    } else {
                        mainWindow.toggleWidgetMode();
                    }
                }
                flat: true
                implicitWidth: 30
                background: Rectangle {
                    color: "transparent"
                }
            }

            Button {
                text: "+"
                onClicked: addTaskPopup.visible ? addTaskPopup.close() : addTaskPopup.open()
                flat: true
                visible: isDesktopWidget
                implicitWidth: 30
                background: Rectangle {
                    color: "transparent"
                }
            }

            // å…³é—­æŒ‰é’®
            Button {
                text: "âœ•"
                onClicked: root.close()
                flat: true
                visible: !isDesktopWidget
                implicitWidth: 30
                background: Rectangle {
                    color: "transparent"
                }
            }
        }
    }

    // è¾¹æ¡†è°ƒæ•´å¤§å°åŒºåŸŸ
    // å·¦è¾¹æ¡†
    MouseArea {
        id: leftResizeArea
        width: resizeBorderWidth
        anchors.left: parent.left
        anchors.top: parent.top
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeHorCursor

        property string edge: "left"

        onPressed: {
            root.startSystemResize(Qt.LeftEdge);
        }
    }

    // å³è¾¹æ¡†
    MouseArea {
        id: rightResizeArea
        width: resizeBorderWidth
        anchors.right: parent.right
        anchors.top: parent.top
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeHorCursor

        property string edge: "right"

        onPressed: {
            root.startSystemResize(Qt.RightEdge);
        }
    }

    // ä¸Šè¾¹æ¡†
    MouseArea {
        id: topResizeArea
        height: resizeBorderWidth
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.right: parent.right
        visible: !isDesktopWidget
        cursorShape: Qt.SizeVerCursor

        property string edge: "top"

        onPressed: {
            root.startSystemResize(Qt.TopEdge);
        }
    }

    // ä¸‹è¾¹æ¡†
    MouseArea {
        id: bottomResizeArea
        height: resizeBorderWidth
        anchors.bottom: parent.bottom
        anchors.left: parent.left
        anchors.right: parent.right
        visible: !isDesktopWidget
        cursorShape: Qt.SizeVerCursor

        property string edge: "bottom"

        onPressed: {
            root.startSystemResize(Qt.BottomEdge);
        }
    }

    // å·¦ä¸Šè§’
    MouseArea {
        id: topLeftResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.left: parent.left
        anchors.top: parent.top
        visible: !isDesktopWidget
        cursorShape: Qt.SizeFDiagCursor

        property string edge: "topleft"

        onPressed: {
            root.startSystemResize(Qt.TopEdge | Qt.LeftEdge);
        }
    }

    // å³ä¸Šè§’
    MouseArea {
        id: topRightResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.right: parent.right
        anchors.top: parent.top
        visible: !isDesktopWidget
        cursorShape: Qt.SizeBDiagCursor

        property string edge: "topright"

        onPressed: {
            root.startSystemResize(Qt.TopEdge | Qt.RightEdge);
        }
    }

    // å·¦ä¸‹è§’
    MouseArea {
        id: bottomLeftResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.left: parent.left
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeBDiagCursor

        property string edge: "bottomleft"

        onPressed: {
            root.startSystemResize(Qt.BottomEdge | Qt.LeftEdge);
        }
    }

    // å³ä¸‹è§’
    MouseArea {
        id: bottomRightResizeArea
        width: resizeBorderWidth
        height: resizeBorderWidth
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        visible: !isDesktopWidget
        cursorShape: Qt.SizeFDiagCursor

        property string edge: "bottomright"

        onPressed: {
            root.startSystemResize(Qt.BottomEdge | Qt.RightEdge);
        }
    }

    // å¯¼èˆªæ ˆï¼Œä½¿ç”¨å†…ç½® Page è¿›è¡Œé¡µé¢ç®¡ç†
    StackView {
        id: stackView
        anchors.top: titleBar.bottom
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        z: 1000
        focus: true
        visible: depth > 0 // ä»…åœ¨æœ‰é¡µé¢æ—¶æ˜¾ç¤º
        initialItem: mainPageComponent
    }

    // é¡¶éƒ¨ç”¨æˆ·èœå•ï¼ˆä»å¤´åƒå¤„ç‚¹å‡»å¼¹å‡ºï¼‰
    Menu {
        id: topMenu

        MenuItem {
            text: qsTr("è®¾ç½®")
            onTriggered: {
                stackView.push(Qt.resolvedUrl("Setting.qml"), {
                    isDarkMode: root.isDarkMode,
                    rootWindow: root
                });
            }
        }

        MenuItem {
            text: root.isDarkMode ? qsTr("åˆ‡æ¢ä¸ºæµ…è‰²æ¨¡å¼") : qsTr("åˆ‡æ¢ä¸ºæ·±è‰²æ¨¡å¼")
            onTriggered: root.isDarkMode = !root.isDarkMode
        }

        MenuItem {
            text: (todoModel.isOnline ? qsTr("è‡ªåŠ¨åŒæ­¥: å¼€") : qsTr("è‡ªåŠ¨åŒæ­¥: å…³"))
            onTriggered: todoModel.isOnline = !todoModel.isOnline
        }
    }
    // é€šç”¨ç¡®è®¤å¯¹è¯æ¡†ç»„ä»¶
    Component {
        id: confirmDialogComponent

        Dialog {
            id: confirmDialog
            title: qsTr("ç¡®è®¤")
            standardButtons: Dialog.NoButton
            modal: true

            property string message: ""
            property string yesButtonText: qsTr("ç¡®å®š")
            property string noButtonText: qsTr("å–æ¶ˆ")

            contentItem: ColumnLayout {
                spacing: 20

                Label {
                    text: confirmDialog.message
                    wrapMode: Text.WordWrap
                    Layout.fillWidth: true
                }

                RowLayout {
                    Layout.alignment: Qt.AlignRight
                    spacing: 10

                    Button {
                        text: confirmDialog.noButtonText
                        onClicked: confirmDialog.reject()
                    }

                    Button {
                        text: confirmDialog.yesButtonText
                        onClicked: confirmDialog.accept()
                    }
                }
            }
        }
    }

    Component {
        id: mainPageComponent
        MainPage {
            isDarkMode: root.isDarkMode
            isDesktopWidget: root.isDesktopWidget
            rootWindow: root
        }
    }
}
